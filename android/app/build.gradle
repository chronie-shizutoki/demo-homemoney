import java.util.*

plugins {
    id 'com.android.application'
    id 'com.google.devtools.ksp'
    id 'com.google.dagger.hilt.android'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlin.plugin.compose'
}

// 使用更兼容的日期格式化方式
def getBuildDate() {
    def calendar = Calendar.getInstance()
    def year = calendar.get(Calendar.YEAR)
    def month = calendar.get(Calendar.MONTH) + 1
    def day = calendar.get(Calendar.DAY_OF_MONTH)
    return String.format("%04d%02d%02d", year, month, day)
}

def getBuildTime() {
    def calendar = Calendar.getInstance()
    def hour = calendar.get(Calendar.HOUR_OF_DAY)
    def minute = calendar.get(Calendar.MINUTE)
    return String.format("%02d%02d", hour, minute)
}

android {
    namespace "com.chronie.homemoney.demo"
    compileSdk rootProject.ext.compileSdkVersion
    
    defaultConfig {
        applicationId "com.chronie.homemoney.demo"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        
        def buildDate = getBuildDate()
        def defaultBuildNumber = getBuildTime()
        def buildNumber = project.hasProperty("buildNumber") ? project.buildNumber : defaultBuildNumber
        
        versionCode = (System.currentTimeMillis() / 1000).intValue()
        versionName = "1.${buildDate}.${buildNumber}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
        
        vectorDrawables {
            useSupportLibrary true
        }
        
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    
    buildFeatures {
        compose true
    }

    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'
    
    // Android Core
    implementation "androidx.core:core-ktx:$androidxCoreVersion"
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation 'com.google.android.material:material:1.13.0'
    
    // Jetpack Compose
    implementation platform('androidx.compose:compose-bom:2026.01.01')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'

    // Enable Material 3 Experimental Features
    implementation 'androidx.compose.material3:material3:1.5.0-alpha13'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.compose.material:material:1.8.0'
    implementation "androidx.activity:activity-compose:$androidxActivityVersion"
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.10.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.10.0'
    
    // Navigation
    implementation 'androidx.navigation:navigation-compose:2.9.7'
    
    // Hilt
    implementation 'com.google.dagger:hilt-android:2.59.1'
    ksp 'com.google.dagger:hilt-compiler:2.59.1'
    implementation 'androidx.hilt:hilt-navigation-compose:1.3.0'
    
    // DataStore
    implementation 'androidx.datastore:datastore-preferences:1.2.0'
    
    // Room
    implementation 'androidx.room:room-runtime:2.8.4'
    implementation 'androidx.room:room-ktx:2.8.4'
    ksp 'androidx.room:room-compiler:2.8.4'
    
    // Security
    implementation 'androidx.security:security-crypto:1.1.0'
    implementation 'net.zetetic:android-database-sqlcipher:4.5.4'
    implementation 'androidx.sqlite:sqlite-ktx:2.6.2'
    
    // WorkManager
    implementation 'androidx.work:work-runtime-ktx:2.11.1'
    implementation 'androidx.hilt:hilt-work:1.3.0'
    ksp 'androidx.hilt:hilt-compiler:1.3.0'
    
    // Apache POI for Excel import/export
    implementation 'org.apache.poi:poi:5.5.1'
    implementation 'org.apache.poi:poi-ooxml:5.5.1'
    
    // Testing
    testImplementation "junit:junit:$junitVersion"
    testImplementation 'io.mockk:mockk:1.14.9'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.2'
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    androidTestImplementation platform('androidx.compose:compose-bom:2026.01.01')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
